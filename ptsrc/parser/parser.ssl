%  PT Parser Version 5.0
%
% Date:  30 Jun 1980
%        (Revised v5.0 - 10 Dec 2020)
%
% Author:  Alan Rosselet
%
% Abstract
%     This module parses the syntax token stream emitted by the scanner
%     and emits a postfix stream of tokens to the semantic analyzer
%     module.  The parser detects and repairs syntax errors so that the
%     token stream emitted to the semantic analyzer is syntactically
%     legal (except for the special case of declaration phase errors - 
%     see the comment in the Block routine below).

Input :
        % Keywords must be first both here and in stdIdentifiers,
        % and they must appear in the exact same order
        % TODO: add comment

        pOr                     'or'
        firstKeywordToken = pOr
        pAnd                    'and'
        pNot                    'not'
        pThen                   'then'
        pElse                   'else'
        pOf                     'of'
        pEnd                    'end'
        pFile                   'file'
        pVar                    'var'
        pIf                     'if'
        pWhile                  'while'
        pRepeat                 'repeat'
        pChoose                 'Choose'
        pElseif                 'elseif'
        pFun                    'fun'
        pIs                     'is'
        pLike                   'like'
        pPkg                    'pkg'
        pPublic                 'public'
        pUsing                  'using'
        pVal                    'val'
        pWhen                   'when'
        lastKeywordToken = pWhen        

        % Scanner tokens -
        %   These are the tokens actually recognized by the Scanner itself.

        % Compound tokens - 
        %   These will be sent to the parser with a value or index.
        pIdentifier 
        firstCompoundInputToken = pIdentifier
        pInteger
        pStringLiteral
        lastCompoundInputToken = pStringLiteral

        % Non-compound tokens - 
        %   These tokens simply represent themselves.
        % TODO: add comment
        pNewLine
        pEndFile
        pPlus                   '+'
        pMinus                  '-'
        pStar                   '*'
        pDot                    '.'
        pComma                  ','
        pSemicolon              ';'
        pColon                  ':'
        pEquals                 '='
        pNotEqual               '!='
        pLess                   '<'
        pLessEqual              '<='
        pGreaterEqual           '>='
        pGreater                '>'
        pLeftParen              '('
        pRightParen             ')'
        pLeftBracket            '['
        pRightBracket           ']'
        pSlash                  '/'
        pBang                   '!' 
        pPercent                '%'
        pHash                   '#'
        pOrBar                  '|'
        pDoubleOrBar            '||'
        pPlusEquals             '+='
        pMinusEquals            '-='
        pStarEquals             '*='
        pSlashEquals            '/='
        pPercentEquals          '%='
        pDoubleEquals           '=='
        lastSyntaxToken = pDoubleEquals;

% TODO: add comment
Output :
        sIdentifier 
        firstSemanticToken = sIdentifier
        firstCompoundSemanticToken = sIdentifier
        sInteger
        sStringLiteral
        lastCompoundSemanticToken = sStringLiteral

        sProgram
        sParmBegin
        sParmEnd
        sLike
        sPackage
        sPublic
        sConcatenate
        sRepeatString
        sSubstring
        sLength
        sInitialValue
        sCaseElse
        sVar
        sVal
        sProcedure
        sBegin
        sEnd
        sNegate
        sArray
        sFile
        sRange
        sCaseStmt
        sCaseEnd
        sLabelEnd
        sExpnEnd
        sNullStmt
        sAssignmentStmt
        sSubscript
        sCallStmt
        sFieldWidth
        sIfStmt
        sThen
        sElse
        sWhileStmt
        sRepeatStmt
        sRepeatEnd
        sEq
        sNE
        sLT
        sLE
        sGT
        sGE
        sAdd
        sSubtract
        sMultiply
        sDivide
        sModulus
        sInfixOr
        sOr
        sInfixAnd
        sAnd
        sNot
        sNewLine
        sEndOfFile
        lastSemanticToken = sEndOfFile;


rules

Program :
        'using'  .sProgram
        % program name
        pIdentifier
        % program parameters
        {
            pIdentifier  .sIdentifier
            [
                | ',':
                | *:
                    >
            ]
        }
        .sParmEnd
        ';'  @Block;

Block :
        % Strictly speaking, standard Pascal requires that declarations
        % appear only in the order consts, types, vars, procedures.
        % We allow arbitrary intermixing of these instead to preserve
        % programmer sanity.
        .sBegin
        {[
            | 'public':
                [
                    | 'val':
                        .sVal
                        .sPublic
                        @ConstantDefinitions
                    | 'var':
                        .sVar
                        .sPublic
                        @VariableDeclarations
                    | 'fun':
                        .sProcedure
                        .sPublic
                        @ProcedureHeading 'is'
                        @Block      
                        'end' ';'
                ]
            | 'val':
                .sVal
                @ConstantDefinitions
            | 'var':
                .sVar
                @VariableDeclarations
            | 'fun':
                .sProcedure
                % procedure name
                pIdentifier  .sIdentifier
                @ProcedureHeading 'is'
                @Block
                'end' ';'
            | 'pkg':
                .sPackage
                pIdentifier  .sIdentifier 'is'
                @Block
                'end' ';'
            | pIdentifier:
                @AssignmentOrCallStmt
            | pStringLiteral:
                @Expression
            | 'if':
                @IfStmt
            | 'choose':
                @CaseStmt
            | 'repeat':
                [
                    | 'while':
                        @WhileStmt
                    | *:
                        @RepeatStmt
                ]
            | ';':
                .sNullStmt
            | *:
                >
        ]}
        .sEnd;

ConstantDefinitions :
        % Accept one or more named constant definitions
        pIdentifier  .sIdentifier
        '=' @ConstantValue % e.g. var a=1, b=2;
        {[
            | ';':
                >
            | *:
                ',' pIdentifier .sIdentifier
                '=' @ConstantValue
        ]};

ConstantValue :
        [
            | pInteger:
                .sInteger
            | pIdentifier:
                .sIdentifier
            | '-':
                @UnsignedIntegerConstant
                .sNegate
            | '+':
                @UnsignedIntegerConstant
            | pStringLiteral:
                .sStringLiteral
        ];

UnsignedIntegerConstant :
        [
            | pIdentifier:
                .sIdentifier
            | pInteger:
                .sInteger
        ];

ValueOrLike :
        [
            | '=':
                .sInitialValue
                @Expression
                .sExpnEnd              
            | *:
                @LikeClause
        ];

LikeClause:
        [
            | ':':
                [
                    | 'file':
                        .sFile 'like' .sLike @VariableOrConstant
                    | *:
                        'like' .sLike @VariableOrConstant
                ]
            | '[':
                .sArray
                @constantValue ']' ':'
                [
                    | 'file':
                        .sFile 'like' .sLike @VariableOrConstant
                    | *:
                        'like' .sLike @VariableOrConstant
                ]
        ];

VariableOrConstant:
        [
            | pIdentifier:
                .sIdentifier
            | *:
                @ConstantValue
        ];

OptionallySignedIntegerConstant :
        [
            | '-':
                @UnsignedIntegerConstant
                .sNegate
            | '+':
                @UnsignedIntegerConstant
            | *:
                @UnsignedIntegerConstant
        ];

VariableDeclarations :
        % Accept only one declaration.
        @ValueOrLike;

ProcedureHeading :
        % Accept zero or more procedure formal parameter declarations.
        [
            | '(':
                {
                    % formal parameter identifier
                    [
                        | 'var':
                            pIdentifier  .sIdentifier
                            .sVar
                        | *:
                            pIdentifier  .sIdentifier
                    ]
                    @LikeClause
                    [
                        | ',':
                        | *:
                            >
                    ]
                }
                ')'
            | *:
        ]
        .sParmEnd;

AssignmentOrCallStmt :
        [
            | '=':
                .sAssignmentStmt
                .sIdentifier    % identifier accepted in the Statement rule
                @Expression
                .sExpnEnd
            | '[':
                .sAssignmentStmt
                .sIdentifier
                .sSubscript
                @Expression
                .sExpnEnd
                ']'  '='
                @Expression
                .sExpnEnd
            | '+=':
                .sAssignmentStmt
                .sIdentifier
                .sIdentifier
                @Expression
                .sAdd
                .sExpnEnd
            | '-=':
                .sAssignmentStmt
                .sIdentifier
                .sIdentifier
                @Expression
                .sSubtract
                .sExpnEnd
            | '*=':
                .sAssignmentStmt
                .sIdentifier
                .sIdentifier
                @Expression
                .sMultiply
                .sExpnEnd
            | '/=':
                .sAssignmentStmt
                .sIdentifier
                .sIdentifier
                @Expression
                .sDivide
                .sExpnEnd
            | '%=':
                .sAssignmentStmt
                .sIdentifier
                .sIdentifier
                @Expression
                .sModulus
                .sExpnEnd
            | *:
                @CallStmt
        ] ';';

CallStmt :
        .sCallStmt
        .sIdentifier    % procedure identifier accepted in the Statement rule
        % accept zero or more actual parameters
        [
            | '(':
                {
                    @Expression
                    .sExpnEnd
                    % allow a field width specification to occur in calls
                    % to the standard procedures write and writeln
                    [
                        | ':':
                            .sFieldWidth
                            @Expression
                            .sExpnEnd
                        | *:
                    ]
                    [
                        | ',':
                        | *:
                            >
                    ]
                }
                ')'
            | *:        % no actual parameters
        ]
        .sParmEnd;

IfStmt :
        .sIfStmt
        @Expression
        .sExpnEnd
        'then'  .sThen
        @Block
        [
            | 'elseif':
                .sElse
                .sBegin
                @IfStmt
                .sEnd
            | *:
               [
                   | 'else':
                       .sElse
                       @Block
                   | *:
                ]
                'end' ';'
        ];

CaseStmt :
        .sCaseStmt
        @Expression
        .sExpnEnd
        'of'
        'when'
        @CaseAlternative
        {[
            | 'when':
                @CaseAlternative
            | *:
                >
        ]}
        .sCaseEnd
        [
            | 'else':
                .sCaseElse
                @Block
            | *:
        ]
        'end' ';';

CaseAlternative :
        % A case alternative is a statement labelled by
        % one or more optionally signed integer constants

        {
            @OptionallySignedIntegerConstant
            [
                | ',':
                | *:
                    >
            ]
        }
        .sLabelEnd
        'then'  @Block ;

WhileStmt :
        .sWhileStmt
        @Expression
        .sExpnEnd
        @Block
        'end' ';';

RepeatStmt :
        .sRepeatStmt
        {
            @Block
            [
                | 'while':
                    .sRepeatEnd
                    >
            ]
        }
        @Expression
        ';'
        .sNot
        .sExpnEnd;

Expression :
        @SimpleExpression
        [
            | '==':
                @SimpleExpression  .sEq
            | '!=':
                @SimpleExpression  .sNE
            | '<':
                @SimpleExpression  .sLT
            | '<=':
                @SimpleExpression  .sLE
            | '>':
                @SimpleExpression  .sGT
            | '>=':
                @SimpleExpression  .sGE
            | *:
        ];

SimpleExpression :
        [
            | '+':
                @Term
            | '-':
                @Term  .sNegate
            | *:
                @Term
        ]
        {[
            | '+':
                @Term  .sAdd
            | '-':
                @Term  .sSubtract
            | 'or':
                .sInfixOr  @Term  .sOr
            | '|':
                @Term  .sConcatenate
            | '||':
                @Term  .sRepeatString
            | *:
                >
        ]};

Term :
        @Factor
        {[
            | '*':
                @Factor  .sMultiply
            | '/':
                @Factor  
                [
                    | ':':
                        pInteger .sInteger
                        .sSubstring
                    | *:
                        .sDivide
                ]
            | '%':
                @Factor  .sModulus
            | 'and':
                .sInfixAnd  @Factor  .sAnd
            | *:
                >
        ]};

Factor :
        [
            | pIdentifier:
                .sIdentifier
                @IdentifierExtension
            | pInteger:
                .sInteger
            | '(':
                @Expression  ')'
            | 'not':
                @Factor
                .sNot
            | '#':
                @Factor
                .sLength
            | pStringLiteral:
                .sStringLiteral
            | 'file':
                .sFile '(' @Expression ')'
                .sExpnEnd
        ];

IdentifierExtension :
        % Handle subscripts and function actual parameter lists
        [
            | '[':
                .sSubscript
                @Expression  ']'
                .sExpnEnd
            | '(':
                .sParmBegin
                @Expression  ')'
                .sExpnEnd
                .sParmEnd
            | *:
        ];

end
